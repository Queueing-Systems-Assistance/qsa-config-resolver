plugins {
	id "org.sonarqube" version "2.7"
	id("maven-publish")
}

ext {
	/* Plugin Information */
	jdkVersion = '11'
	checkstyleVersion = '8.17'
	/* Project Information */
	projectGroup = 'com.unideb.qsa'
	projectMajorVersion = '1'
	projectMinorVersion = '0'
	projectBuildVersion = System.getenv('TRAVIS_BUILD_NUMBER') ? System.getenv('TRAVIS_BUILD_NUMBER') : '0-SNAPSHOT'
	projectVersion = projectMajorVersion + '.' + projectMinorVersion + '.' + projectBuildVersion
	/* Secrets */
	githubToken = System.getenv('GITHUB_TOKEN')
	githubUsername = System.getenv('GITHUB_USERNAME')
}

group = projectGroup
version = projectVersion

apply plugin: 'maven'
apply plugin: 'checkstyle'
apply plugin: 'java'
apply plugin: 'java-library'

repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
	maven {
		url 'https://jitpack.io'
	}
}

checkstyle {
	setToolVersion(checkstyleVersion)
	configFile = file("${rootDir}/support/code-formatting/checkstyle/checkstyle.xml")
	configDir = file("${rootDir}/support/code-formatting/checkstyle/")
	showViolations = true
	ignoreFailures = false
}

sonarqube {
	properties {
		property 'sonar.projectVersion', projectVersion
	}
}

dependencies {
	implementation group: 'com.google.guava', name: 'guava', version: '14.0.1'
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.5'
	implementation group: 'org.springframework', name: 'spring-core', version: '4.2.7.RELEASE'
	implementation group: 'com.github.everit-org.json-schema', name: 'org.everit.json.schema', version: '1.11.0'
	implementation group: 'org.apache.commons', name: 'commons-text', version: '1.6'
	implementation group: 'commons-io', name: 'commons-io', version: '2.6'
}

publishing {
	repositories {
		maven {
			name = 'GitHubPackages'
			url = uri('https://maven.pkg.github.com/queueing-systems-assistance/qsa-config-domain')
			credentials {
				username = githubUsername
				password = githubToken
			}
		}
	}
	publications {
		gpr(MavenPublication) {
			from(components.java)
		}
	}
}

sourceCompatibility = jdkVersion

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}
