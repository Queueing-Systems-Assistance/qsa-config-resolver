plugins {
  id 'maven-publish'
}

ext {
  /* Project Information */
  projectGroup = 'com.unideb.qsa'
  projectVersion = project.getProperties().get('release.version') ?: 'RELEASE.VERSION.UNKNOWN'
  /* Secrets */
  // Note: Use your username and token
  githubToken = System.getenv('GITHUB_TOKEN') ?: 'YOUR_TOKEN'
  githubUsername = System.getenv('GITHUB_USERNAME') ?: 'YOUR_USERNAME'
  print('Release version [' + projectVersion + ']')
}

apply plugin: 'maven-publish'
apply plugin: 'checkstyle'
apply plugin: 'java'
apply plugin: 'java-library'

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url 'https://jitpack.io'
  }
}

checkstyle {
  toolVersion = '9.2'
  configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
  configDirectory = file("${rootDir}/config/checkstyle/")
  showViolations = true
  ignoreFailures = false
}

dependencies {
  implementation 'com.google.code.gson:gson:2.10.1'
  testImplementation 'org.mockito:mockito-core:5.1.1'
  testImplementation 'org.testng:testng:7.7.0'
}

publishing {
  repositories {
    maven {
      name = 'GitHubPackages'
      url = uri('https://maven.pkg.github.com/queueing-systems-assistance/qsa')
      credentials {
        username = githubUsername
        password = githubToken
      }
    }
  }
  publications {
    gpr(MavenPublication) {
      from(components.java)
    }
  }
}

sourceCompatibility = '17'
group = projectGroup
version = projectVersion

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

test {
  useTestNG()
  scanForTestClasses = false
  testLogging {
    afterSuite { desc, result ->
      if (!desc.parent) {
        println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
      }
    }
  }
}